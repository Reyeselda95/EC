* Library of optocoupler models

* Copyright 1989-1995 by MicroSim Corporation
* Neither this library nor any part may be copied without the express
* written consent of MicroSim Corporation

* $Revision:   1.15  $
* $Author:   ANW  $
* $Date:   23 Feb 1995 18:39:14  $

* ---------------------------------------------------------------------------
* The parameters in this model library were derived from the data sheets for
* each part.
* ---------------------------------------------------------------------------
*$
*.model A4N25
* 6-pin DIP: pin #1   #2   #4   #5   #6
*		 |    |    |    |    |
.subckt A4N25	pin1 pin2 pin4 pin5 pin6	params: rel_CTR=1
*		Motorola	pid=4N25
*		88-01-04 pwt
*		88-01-18 pwt	rework Cje approximation

* The data sheet used for this model is from Motorola: it was the most
* complete for DC and switching parameters, and is was easy to find the
* component IR-LED and phototransistor as separate devices for further
* specifications.

  d_MainLED	pin1 pin2	MainLED
  d_PhotoLED	pin1 1		PhotoLED .001
  v_PhotoLED	1 pin2		0

  f_TempComp	0 2		v_PhotoLED 1.7
  r_TempComp	2 0		TempComp {rel_CTR}

  g_BaseSrc	5 6 2 0		.9
  q_PhotoBJT	5 6 4		PhotoBJT
  r_C		5 pin5		.1
  r_B		6 pin6		.1
  r_E		4 pin4		.1

* Since active devices dominate pin-to-pin capacitance on each "side" of the
* optocoupler, isolation is modeled by identical capacitances and resistances
* linked to a common point; this gives isolation of .5pF and 1E+11 ohms
  c_1		pin1 7		.4p
  r_1		pin1 7		.12T
  c_2		pin2 7		.4p
  r_2		pin2 7		.12T
  c_4		pin4 7		.4p
  r_4		pin4 7		.12T
  c_5		pin5 7		.4p
  r_5		pin5 7		.12T
  c_6		pin6 7		.4p
  r_6		pin6 7		.12T

* Similar to Motorola MLED15.
.model MainLED  D(Is=10.01e-21 Rs=2.049 Ikf=11.84 N=1.053 Xti=3 Cjo=40p M=.34 
+                Vj=.75 Isr=30n Nr=3.8 Bv=6 Ibv=100u Tt=.5u)

* Models photon generation: same as MainLED except no AC effects, no breakdown.
.model PhotoLED D(Is=1.1p Rs=.66 Ikf=30m N=1.9 Xti=3 Cjo=0   M=.34 Vj=.75
+		Isr=30n Nr=3.8 Bv=0 Tt=0)

* Temperature compensation for system: 1.38x @ -55'C, .54x @ +100'C, all @ 10mA
* Note: the photo BJT has its own temperature corrections, which must be kept
* as the transistor is electrically available.
.model TempComp	RES(R=1 Tc1=-11.27m Tc2=43.46u)

* Similar to Motorola MDR3050; Hfe=325 @ Ic=500uA, Vce=5V
* Use beta variation (w/Parts) to model change in current-transfer ratio (CTR).
* Hand adjust reverse beta (Br) to match saturation characteristics.
* Set Isc to model dark current.
* Hand adjust Cjc to match fall time @ Ic=10mA (which yields rise time, too).
* Hand adjust reverse transit-time (Tr) to match storage time @ Ic=10mA.
* Delay time set by LED I-V and C-V characteristics; set Cje to 25% of Cjc,
* inspection of phototransistor chip layouts show the emitter area is 20%-25%
* that of the collector area.  The same layouts show that base resistance is
* made negligible by design; also, the operating currents are small.
* Hand adjust forward transit-time (Tf) to match MDR3050 pulse data.  Check
* against 4N25 frequency response (Fig 11, 12).
.model PhotoBJT NPN(Is=10f Xti=3 Vaf=60
+		Bf=400 Ne=3.75 Ise=580p Ikf=.26 Xtb=1.5
+		Br=.04 Nc=2    Isc=3.5n
+		Cjc=10p  Mjc=.3333 Vjc=.75 Tr=88u
+		Cje=2.5p Mje=.3333 Vje=.75 Tf=1.5n)
.ends

*$
*.model A4N25A
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt A4N25A	pin1 pin2 pin4 pin5 pin6   params: rel_CTR=1
*		88-01-05 pwt
* Same as 4N25 (UL recognized).
  x1 pin1 pin2 pin4 pin5 pin6 A4N25  params:  rel_CTR={rel_CTR}
.ends

*$
*.model A4N26
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt A4N26	pin1 pin2 pin4 pin5 pin6     params: rel_CTR=1
*		88-01-05 pwt
* Same as 4N25, lower isolation breakdown voltage.
  x1 pin1 pin2 pin4 pin5 pin6 A4N25      params:  rel_CTR={rel_CTR}
.ends

*$
*.model A4N27
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt A4N27	pin1 pin2 pin4 pin5 pin6    params: rel_CTR=0.5
*		88-01-05 pwt
* Same as 4N25, lower CTR, lower isolation breakdown voltage.
  x1 pin1 pin2 pin4 pin5 pin6 A4N25   params:  rel_CTR={rel_CTR}
.ends

*$
*.model A4N28
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt A4N28	pin1 pin2 pin4 pin5 pin6  params: rel_CTR=0.5
*		88-01-05 pwt
* Same as 4N25, lower CTR, lower isolation breakdown voltage.
  x1 pin1 pin2 pin4 pin5 pin6 A4N25    params: rel_CTR={rel_CTR}
.ends

*$
*.model H11A2
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt H11A2	pin1 pin2 pin4 pin5 pin6    params: rel_CTR=1
*		88-01-05 pwt
* Every manufacturer seems to use their own 4N25 as an equivalent device.
  x1 pin1 pin2 pin4 pin5 pin6 A4N25     params: rel_CTR={rel_CTR}
.ends

*$
*.model H11A3
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt H11A3	pin1 pin2 pin4 pin5 pin6    params: rel_CTR=1
*		88-01-05 pwt
* Same as H11A2, higher isolation breakdown voltage.
  x1 pin1 pin2 pin4 pin5 pin6 A4N25     params: rel_CTR={rel_CTR}
.ends

*$
*.model H11A4
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt H11A4	pin1 pin2 pin4 pin5 pin6    params: rel_CTR=1
*		88-01-05 pwt
* Every manufacturer seems to use their own 4N27 as an equivalent device.
  x1 pin1 pin2 pin4 pin5 pin6 A4N27    params: rel_CTR={rel_CTR}
.ends

*$
*.model H11A520
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt H11A520	pin1 pin2 pin4 pin5 pin6   params: rel_CTR=1
*		88-01-05 pwt
* Same as H11A2, higher isolation breakdown voltage.
  x1 pin1 pin2 pin4 pin5 pin6 A4N25     params: rel_CTR={rel_CTR}
.ends

*$
*.model MCT2
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt MCT2	pin1 pin2 pin4 pin5 pin6   params: rel_CTR=1
*		88-01-05 pwt
* Every manufacturer seems to use their own 4N25 as an equivalent to the
* General Instruments device.
  x1 pin1 pin2 pin4 pin5 pin6 A4N25    params: rel_CTR={rel_CTR}
.ends

*$
*.model MCT2E
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt MCT2E	pin1 pin2 pin4 pin5 pin6    params: rel_CTR=1
*		88-01-05 pwt
* Same as MTC2E, higher isolation breakdown voltage.
  x1 pin1 pin2 pin4 pin5 pin6 A4N25    params: rel_CTR={rel_CTR}
.ends

*$
*.model MOC1005
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt MOC1005	pin1 pin2 pin4 pin5 pin6   params: rel_CTR=1
*		88-01-05 pwt
* Motorola equivalent of 4N25
  x1 pin1 pin2 pin4 pin5 pin6 A4N25    params: rel_CTR={rel_CTR}
.ends

*$
*.model MOC1006
* 6-pin DIP: pin #1   #2   #4   #5   #6
*                |    |    |    |    |
.subckt MOC1006	pin1 pin2 pin4 pin5 pin6    params: rel_CTR=1
*		88-01-05 pwt
* Motorola equivalent of 4N27
  x1 pin1 pin2 pin4 pin5 pin6 A4N27    params: rel_CTR={rel_CTR}
.ends
*$
